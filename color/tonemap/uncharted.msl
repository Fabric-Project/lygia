/*
contributors: nan
description: 'Uncharted 2 Tonemapping'
use: <float3|float4> tonemapUncharted(<float3|float4> x)
*/

#ifndef FNC_TONEMAPUNCHARTED
#define FNC_TONEMAPUNCHARTED

float3 uncharted2Tonemap(const float3 x) {
    const float A = 0.15;
    const float B = 0.50;
    const float C = 0.10;
    const float D = 0.20;
    const float E = 0.02;
    const float F = 0.30;
    return ((x * (A * x + C * B) + D * E) / (x * (A * x + B) + D * F)) - E / F;
}

float3 tonemapUncharted(const float3 x) {
    const float W = 11.2;
    const float exposureBias = 2.0;
    float3 curr = uncharted2Tonemap(exposureBias * x);
    float3 whiteScale = 1.0 / uncharted2Tonemap(float3(W, W, W));
    return curr * whiteScale;
}

float4 tonemapUncharted(const float4 x) { return float4( tonemapUncharted(x.rgb), x.a); }

half3 uncharted2Tonemap(const half3 x) {
    const half A = 0.15;
    const half B = 0.50;
    const half C = 0.10;
    const half D = 0.20;
    const half E = 0.02;
    const half F = 0.30;
    return ((x * (A * x + C * B) + D * E) / (x * (A * x + B) + D * F)) - E / F;
}

half3 tonemapUncharted(const half3 x) {
    const half W = 11.2;
    const half exposureBias = 2.0;
    half3 curr = uncharted2Tonemap(exposureBias * x);
    half3 whiteScale = 1.0 / uncharted2Tonemap(half3(W, W, W));
    return curr * whiteScale;
}

half4 tonemapUncharted(const half4 x) { return half4( tonemapUncharted(x.rgb), x.a); }

#endif
